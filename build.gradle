plugins {
  id 'org.openapi.generator' version "5.3.0"
  id 'org.springframework.boot' version '2.5.4'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'
}

group = 'mn.erin.dms'
status = 'development'
version = '1.0.0-SNAPSHOT'

repositories {

  mavenCentral()
  maven {
    url "$artifactoryContextUrl/libs-snapshot"
    credentials {
      username = artifactoryUser
      password = artifactoryApiKey
    }
    mavenContent {
      snapshotsOnly()
    }
  }
  maven {
    url "$artifactoryContextUrl/libs-release"
    credentials {
      username = artifactoryUser
      password = artifactoryApiKey
    }
    mavenContent {
      releasesOnly()
    }
  }
}

apply from: 'libs.gradle'
apply plugin: "java"

ext {
  set('springCloudVersion', "2020.0.3")
}
void createOpenApiGenerateTask(String ymlName) {
  String taskName = "openApiGenerate_" + ymlName
  task(taskName, type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "spring"
    inputSpec = "$projectDir/src/main/resources/${ymlName}-api.yaml"
    outputDir = "$buildDir/generated".toString()
    apiPackage = "mn.erin.ees.dms.rest"
    invokerPackage = "mn.erin.ees.dms.rest.invoker"
    modelPackage = "mn.erin.ees.dms.rest.model"
    modelNameSuffix = "RestModel"
    skipOperationExample = true
    skipValidateSpec = false
    configOptions = [
        dateLibrary    : "java8",
        delegatePattern: "true"
    ]
  }
  compileJava.dependsOn(taskName)
}

String[] ymlNames = [
    "document",
    "document-type"
]

for (String ymlName : ymlNames) {
  createOpenApiGenerateTask(ymlName)
}

sourceSets.main {
  java { srcDir "$buildDir/generated/src/main/java" }
}

springBoot {
  mainClass = "mn.erin.ees.document.DocumentApplication"
}

dependencies {
  implementation(lib.commons.lang)

  implementation 'javax.validation:validation-api:2.0.1.Final'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
  implementation 'io.swagger:swagger-annotations:1.5.22'
  implementation 'io.springfox:springfox-swagger2:2.9.2'
  implementation 'io.springfox:springfox-swagger-ui:2.9.2'
  implementation 'org.openapitools:jackson-databind-nullable:0.2.1'
  implementation 'org.jetbrains:annotations:20.1.0'
  implementation ('mn.erin.dms:erin-alfresco-connector:1.1.1')
  {
    exclude(module:'slf4j-simple')
  }

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation(lib.junit5)
  testImplementation(lib.junit5_engine)
  testImplementation(lib.mockito)
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

test {
  useJUnitPlatform()
}
